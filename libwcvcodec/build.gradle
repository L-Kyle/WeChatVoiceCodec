apply plugin: 'com.android.library'

// 添加JCenter插件
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -fexceptions -pthread"
                // 要支持 'armeabi-v7a' 需开启 NO_ASM 宏
                // lame 编解码库的 lame_main.c 在方法 lame_main 中声明的
                // 局部变量占用内存较大，会导致申请的内存超过子线程栈的内存上限从而
                // 导致 SIGSEGV 错误，所以需要减小 MAX_NOGAP 的大小，具体见：
                // [issue2](https://github.com/WuFengXue/WeChatVoiceCodec/issues/2)
                cFlags "-DSTDC_HEADERS -DHAVE_LIMITS_H -DHAVE_MPGLIB -DNO_ASM -DMAX_NOGAP=50"
            }
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

allprojects {
    tasks.withType(Javadoc) {
        options {
            // 兼容中文字符
            encoding "UTF-8"
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/7/docs/api"
            // @date 注解为自定义注解
            tags "date:a:"
        }
    }
}
publish {
    // bintray.com 用户名/组织名 user/org name
    userOrg = 'reinhard'
    // JCenter上显示的路径 path
    groupId = 'com.reinhard'
    // 项目名称 project name
    artifactId = 'WeChatVoiceCodec'
    // 版本号 version code
    publishVersion = '1.0.1'
    // 项目描述 description
    desc = 'WeChat voice encoder and decoder for android'
    // 项目网址链接 link
    website = 'https://github.com/WuFengXue/WeChatVoiceCodec'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
